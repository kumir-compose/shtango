| нужно для работы штанго
использовать Строки
использовать Файлы


|| задать ШТАНГО
|| задать ШТАНГО_ВЕРСИЯ "0.0.1"


|| задать если_ош если ош <> ""



|| задать штанго_контроллер | получить данные \
лит Метод, Путь, Запрос, Заголовки, Тело \
получить веб запрос(Метод, Путь, Запрос, Заголовки, Тело)



|| задать штанго_жсон_парсер | входные данные -- жсон \
лит __штанго_жсон_асд, данные, ош \
__штанго_жсон_асд := считатьЖсон(Тело, ош) \
если_ош то \
    штанго_ответ_400("JSON не валиден") \
    выход \
все \
данные := разложитьЖсон(__штанго_жсон_асд)



|| задать штанго_ответ_400($ответ$) | ответить 400 \
послать веб ответ("400", новыйОбъект, $ответ$)



|| задать штанго_ответ_шаблоном($код$, $шаблон$, $словарь$) \
послать веб ответ($код$, новыйОбъект, шаблон_из_файла($шаблон$, $словарь$))



|| задать штанго_начало_валидации | начать валидацию \
лит __штанго_валидош = "" \
лит __штанго_валид_отчет = ""



|| задать штанго_конец_валидации | конец валидации



|| задать валидация_извлечь_поле($источник$,$ключ$,$переменная$) | извлечь $переменная$ \
если не имеетКлюч($источник$, $ключ$) то \
    __штанго_валид_отчет := "Поле " + $ключ$ + " не найдено" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все \
лит $переменная$ = "" \
$переменная$ := получитьПоКлючу($источник$, $ключ$)



|| задать валидация_целочисл($переменная$, $цел_переменная$, $имя$) | целочисл $переменная$ \
цел $цел_переменная$ \
$цел_переменная$ := лит_в_цел($переменная$, __штанго_валидош) \
если __штанго_валидош <> "" то \
    __штанго_валид_отчет := "Поле " + $имя$ + " не целочисл" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все



|| задать валидация_вещчисл($переменная$, $вещ_переменная$, $имя$) | вещчисл $переменная$ \
вещ $вещ_переменная$ \
$вещ_переменная$ := лит_в_вещ($переменная$, __штанго_валидош) \
если __штанго_валидош <> "" то \
    __штанго_валид_отчет := "Поле " + $имя$ + " не вещчисл" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все



|| задать валидация_лог($переменная$, $лог_переменная$, $имя$) | лог $переменная$ \
лог $лог_переменная$ \
$лог_переменная$ := $переменная$ = "true" \
если $переменная$ <> "true" и $переменная$ <> "false" то \
    __штанго_валид_отчет := "Поле " + $имя$ + " не лог" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все



|| задать валидация_строка_длин($переменная$, $от$, $до$) | длина строки $переменная$ \
если (длин($переменная$) > $до$ и $до$ <> -1) или (длин($переменная$) < $от$ и $от$ <> -1) то \
    __штанго_валид_отчет := "Строка невалидной длины" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все



|| задать валидация_строка_из($переменная$, $символы$) | строка $переменная$ состоит из $символы$ \
если не проверить_строка_из($переменная$, $символы$) то \
    __штанго_валид_отчет := "Строка имеет недопустимые символы" \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все



|| задать валидация_предиката($предикат$, $сообщение$) \
если не $предикат$ то \
    __штанго_валид_отчет := $сообщение$ \
    штанго_ответ_400(__штанго_валид_отчет) \
    выход \
все
